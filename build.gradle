buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        gradlePluginPortal()
        maven {
            url = uri("https://plugins.gradle.org/m2/")
        }
        maven {
            url = 'https://repo.mattmalec.com/repository/releases'
        }
        maven {
            url = "https://maven.mangorage.org/releases"
        }
    }

    dependencies {
        classpath 'com.gradleup.shadow:shadow-gradle-plugin:+'
    }
}

plugins {
    id 'java'
}

apply plugin: 'maven-publish'
apply plugin: 'com.gradleup.shadow'


group = 'org.mangorage'
version = getLatestGitTag() + "." + getLatestGitVersion() + "-beta.1"

println("Version -> " + version)

def getLatestGitTag() {
    def result = "git describe --long --tags".execute().text.trim()
    if (result.empty) {
        throw new RuntimeException("Failed to retrieve commit count")
    } else {
        return result.split("-")[0]
    }
}

def getLatestGitVersion() {
    def result = "git describe --long --tags".execute().text.trim()
    if (result.empty) {
        throw new RuntimeException("Failed to retrieve commit count")
    } else {
        def commitCount = result.split("-")[1].toInteger()
        return commitCount;
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    implementation("com.google.code.gson:gson:2.10.1")
    shadow('com.google.code.gson:gson:2.10.1')
}

test {
    useJUnitPlatform()
}

task sourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

jar {
    manifest {
        attributes(
                'Main-Class': 'org.mangorage.bootstrap.Bootstrap',
                'ModuleMainClass': 'org.mangorage.bootstrap.Bootstrap'
        )
    }
}

shadowJar {
    group "main tasks"
    archiveClassifier.set('')

    excludes.remove('module-info.class')
    relocate('com.google.gson', 'mangobot.bootstrap.internal.shaded.com.google.gson')
    relocate('joptsimple', 'mangobot.bootstrap.internal.shaded.joptsimple')
}

tasks.register('runMain', JavaExec) {
    group = 'application'
    description = 'Runs the main class.'

    classpath = sourceSets.main.runtimeClasspath
    mainClass.set('org.mangorage.bootstrap.Bootstrap') // replace this garbage with your actual main class
}

publishing {
    publications.register("mangobotaddon", MavenPublication) {
        artifactId = 'mangobotbootstrap'
        artifact shadowJar
        artifact sourcesJar

        pom {
            name = 'MangoBotCore'
            description = 'The Main Plugin For MangoBot'
            url = 'https://github.com/MangoRageBot/MangoBot'
            issueManagement {
                system = 'github'
                url = 'https://github.com/MangoRageBot/MangoBot/issues'
            }
            developers {
                developer {
                    id = 'mangorage'
                    name = 'MangoRage'
                }
            }
        }
    }

    repositories {
        maven {
            url = "https://maven.mangorage.org/releases"

            credentials {
                username = System.getenv("MAVEN_USER")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }
}